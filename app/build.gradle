plugins {
    id "com.android.application"
    id "androidx.navigation.safeargs"
}

android {
    namespace "ca.sfu.cmpt276.sudokulang"
    compileSdk 33
    compileSdkPreview "UpsideDownCake"

    defaultConfig {
        applicationId "ca.sfu.cmpt276.sudokulang"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
    }
    buildToolsVersion = "34.0.0-rc2"

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

dependencies {
    def lifecycle_version = "2.6.1"
    def nav_version = "2.6.0-alpha09"
    def room_version = "2.6.0-alpha01"

    implementation "androidx.appcompat:appcompat:1.7.0-alpha02"
    implementation "androidx.constraintlayout:constraintlayout:2.2.0-alpha09"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "com.google.android.material:material:1.10.0-alpha01"

    // Annotation processors.
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // Unit tests.
    testImplementation "org.junit.jupiter:junit-jupiter:5.9.2"
    testImplementation "org.mockito:mockito-junit-jupiter:5.2.0"

    // Instrumented tests.
    androidTestImplementation "androidx.test.ext:junit:1.2.0-alpha01"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.6.0-alpha01"
    androidTestImplementation "androidx.test.uiautomator:uiautomator:2.3.0-alpha02"


    // Fix duplicate classes found.
    // See: https://stackoverflow.com/a/75298544
    //      https://docs.gradle.org/current/userguide/resolution_rules.html#sec:module_replacement
    modules {
        module("org.jetbrains.kotlin:kotlin-stdlib-jdk7") {
            replacedBy("org.jetbrains.kotlin:kotlin-stdlib", "kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        module("org.jetbrains.kotlin:kotlin-stdlib-jdk8") {
            replacedBy("org.jetbrains.kotlin:kotlin-stdlib", "kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}